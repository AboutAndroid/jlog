apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.jiongbull.jlog.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    signingConfigs {
        release {
            storeFile file('../keystore/JiongBull.jks')
            storePassword project.hasProperty('KEYSTORE_PASS') ? KEYSTORE_PASS : System.getenv('KEYSTORE_PASS')
            keyAlias project.hasProperty('ALIAS_NAME') ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty('ALIAS_PASS') ? ALIAS_PASS : System.getenv("ALIAS_PASS")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "jlog_sample.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile project(':jlog')
}